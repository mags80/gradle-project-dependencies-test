group 'group.application'
version '1.0-SNAPSHOT'

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects {
    apply plugin: 'java'
    group 'group.application'
    version '1.0-SNAPSHOT'
    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'

            dependencySubstitution.all { DependencySubstitution dependency ->
                if (dependencyIsForAChangingModule(dependency)) {
                    if (dependency.requested.group == "group.common") {
                        def targetProject = findProject(":${dependency.requested.module}")
                        if (targetProject != null) {
                            dependency.useTarget targetProject
                        }
                    }
                }
            }
        }
    }

}

def boolean dependencyIsForAChangingModule(DependencySubstitution dependency) {
    return dependency.requested instanceof ModuleComponentSelector && dependency.requested.version.endsWith("SNAPSHOT");
}

